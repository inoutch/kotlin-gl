allprojects {
    // TODO remove once https://github.com/Kotlin/kotlin-frontend-plugin/issues/141 is fixed
    plugins.whenPluginAdded { plugin ->
        if (plugin.class.name == 'org.jetbrains.kotlin.gradle.frontend.FrontendPlugin') {
            def fixTask = tasks.register('webpack-config-fix') {
                it.doLast { file('webpack.config.d').mkdir() }
            }
            afterEvaluate {
                tasks.named('webpack-config').configure {
                    it.dependsOn(fixTask)
                }
            }
        }
    }
}

buildscript {
    ext.kotlinVersion = "1.3.61"
    ext.kotlinGLVersion = "0.0.1"

    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.44"
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

// apply kotlin2js
apply plugin: "kotlin2js"

// apply plugin
apply plugin: "org.jetbrains.kotlin.frontend"

// configure kotlin compiler
compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "$project.buildDir.path/js/${project.name}.js"
    kotlinOptions.moduleKind = "commonjs"
    kotlinOptions.main = "call"
}

kotlinFrontend {
    npm {}
    webpackBundle {
        bundleName = "main"
        contentPath = file("src/main/resources")
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion"
    implementation "io.github.inoutch:kotlin-gl-js:$kotlinGLVersion"
}

sourceSets {
    main.kotlin.srcDirs += "../src/commonMain/kotlin"
}
